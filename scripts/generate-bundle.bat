@echo off
setlocal enableDelayedExpansion

rem Run from Qt command prompt with working directory set to root of repo

set BUILD_CONFIG=%1

rem "x64" as the second parameter builds a bundle without ARM64, with the
rem Visual C++ redistributable embedded so it installs offline
if /I "%2"=="x64" (
    set X64_ONLY=1
)

rem Convert to lower case for windeployqt
if /I "%BUILD_CONFIG%"=="debug" (
    set BUILD_CONFIG=debug
    set WIX_MUMS=10
) else (
    if /I "%BUILD_CONFIG%"=="release" (
        set BUILD_CONFIG=release
        set WIX_MUMS=10
    ) else (
        if /I "%BUILD_CONFIG%"=="signed-release" (
            set BUILD_CONFIG=release
            set SIGN=1
            set MUST_DEPLOY_SYMBOLS=1

            rem Fail if there are unstaged changes
            git diff-index --quiet HEAD --
            if !ERRORLEVEL! NEQ 0 (
                echo Signed release builds must not have unstaged changes!
                exit /b 1
            )
        ) else (
            echo Invalid build configuration - expected 'debug' or 'release'
            exit /b 1
        )
    )
)

set SIGNTOOL_PARAMS=sign /tr http://timestamp.digicert.com /td sha256 /fd sha256 /sha1 8b9d0d682ad9459e54f05a79694bc10f9876e297 /v

set BUILD_ROOT=%cd%\build
set SOURCE_ROOT=%cd%
set BUILD_FOLDER=%BUILD_ROOT%\build-%BUILD_CONFIG%
set INSTALLER_FOLDER=%BUILD_ROOT%\installer-%BUILD_CONFIG%

rem Allow CI to override the version.txt with an environment variable
if defined CI_VERSION (
    set VERSION=%CI_VERSION%
) else (
    set /p VERSION=<%SOURCE_ROOT%\app\version.txt
)

rem Ensure that all architectures have been built before the final bundle
if not exist "%BUILD_ROOT%\build-x64-%BUILD_CONFIG%\KanePlay.msi" (
    echo Unable to build bundle - missing binaries for %BUILD_CONFIG% x64
    echo You must run 'build-arch.bat %BUILD_CONFIG% x64' first
    exit /b 1
)
if not defined X64_ONLY if not exist "%BUILD_ROOT%\build-arm64-%BUILD_CONFIG%\KanePlay.msi" (
    echo Unable to build bundle - missing binaries for %BUILD_CONFIG% arm64
    echo You must run 'build-arch.bat %BUILD_CONFIG% arm64' first
    exit /b 1
)

echo Cleaning output directories
rmdir /s /q %BUILD_FOLDER%
rmdir /s /q %INSTALLER_FOLDER%
mkdir %BUILD_FOLDER%
mkdir %INSTALLER_FOLDER%

rem Find Visual Studio and run vcvarsall.bat
call "%SOURCE_ROOT%\scripts\find-vswhere.bat"
if !ERRORLEVEL! NEQ 0 goto Error
for /f "usebackq delims=" %%i in (`%VSWHERE% -latest -property installationPath`) do (
    call "%%i\VC\Auxiliary\Build\vcvarsall.bat" x86
)
if !ERRORLEVEL! NEQ 0 goto Error

if defined X64_ONLY (
    set VCREDIST_X64_PATH=!VCToolsRedistDir!vc_redist.x64.exe
    if not exist "!VCREDIST_X64_PATH!" (
        echo Unable to find !VCREDIST_X64_PATH!
        goto Error
    )

    rem major.minor.<days since 2026>.<minute of the day>, so that each build
    rem replaces the previous one (installer version fields stop at 65535)
    for /f %%i in ('powershell -NoProfile -Command "[string]::Format('{0}.{1}.{2}.{3}', '%VERSION%'.Split('.')[0], '%VERSION%'.Split('.')[1], [math]::Floor(((Get-Date) - (Get-Date '2026-01-01')).TotalDays), (Get-Date).Hour * 60 + (Get-Date).Minute)"') do set BUNDLE_VERSION=%%i
    echo Bundle version !BUNDLE_VERSION!
)

echo Building bundle
rem Bundles are always x86 binaries
cmd /c "set VERSION= && msbuild -Restore %SOURCE_ROOT%\wix\KanePlaySetup\KanePlaySetup.wixproj /p:Configuration=%BUILD_CONFIG% /p:Platform=x86 /p:MSBuildProjectExtensionsPath=%BUILD_FOLDER%\"
if !ERRORLEVEL! NEQ 0 goto Error

rem Rename the installer to match the publishing convention
ren %INSTALLER_FOLDER%\KanePlaySetup.exe KanePlaySetup-%VERSION%.exe

echo Build successful for KanePlay v%VERSION% installer!
exit /b 0

:Error
echo Build failed!
exit /b !ERRORLEVEL!
